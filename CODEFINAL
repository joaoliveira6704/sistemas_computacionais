#include <DHT.h>
#include <SoftwareSerial.h>
#include <EEPROM.h>

// Definir PIN e tipo sensor
#define DHTPIN 4       // Pino DATA
#define DHTTYPE DHT11  // Tipo de sensor

#define BUZZERPIN 8

// Definir PIN LEDS
const int LED_GREEN = 10; // Led Verde
const int LED_RED = 9;    // Led Vermelho

// Bluetooth module pins (RX and TX)
#define BT_RX 0
#define BT_TX 1

// Inicializar o sensor
DHT dht(DHTPIN, DHTTYPE);

// Variáveis globais para armazenar valores
float heatIndex = 0;
float temperature = 0;
float humidity = 0;
int gasVal = 0;
int alcVal = 0;
String timestamp = "2000-01-01 00:00:00"; // Data/hora inicial

// Limites normais
const float TEMP_MIN = 15.0; // Temperatura mínima (em °C)
const float TEMP_MAX = 30.0; // Temperatura máxima (em °C)
const float HUM_MIN = 30.0;  // Humidade mínima (em %)
const float HUM_MAX = 70.0;  // Humidade máxima (em %)
const int GAS_MAX = 300;     // Valor máximo aceitável do gás
const int ALC_MAX = 200;     // Valor máximo aceitável de álcool

// Variáveis EEPROM
int eepromIndex = 0; // Índice para a próxima gravação
int eepromMax = EEPROM.length(); // Tamanho total da EEPROM

SoftwareSerial bluetooth(BT_RX, BT_TX); // RX, TX

void setup() {
  Serial.begin(9600);
  bluetooth.begin(9600);

  pinMode(LED_GREEN, OUTPUT);
  pinMode(LED_RED, OUTPUT);
  pinMode(BUZZERPIN, OUTPUT);
  digitalWrite(LED_GREEN, LOW);
  digitalWrite(LED_RED, LOW);
  

  Serial.println("Dispositivo iniciado. Aguarde comandos Bluetooth.");
  dht.begin();
}

void loop() {
  // Verificar comandos Bluetooth
  if (bluetooth.available()) {
    String command = bluetooth.readStringUntil('\n');
    command.trim(); // Remove espaços extras
    processCommand(command);
  }

  // Lógica de leitura e gravação
  readSensors();
  checkValues(); // Verificar se os valores estão normais
  saveToEEPROM();
  delay(2000); // Intervalo de 2 segundos
}

// Função para processar comandos Bluetooth
void processCommand(String command) {
  if (command.startsWith("SETTIME")) {
    timestamp = command.substring(8); // Pega tudo após "SETTIME "
    Serial.print("Hora definida para: ");
    Serial.println(timestamp);
    bluetooth.print("Hora definida para: ");
    bluetooth.println(timestamp);
  } else if (command == "ERASE") {
    for (int i = 0; i < eepromMax; i++) {
      EEPROM.write(i, 0);
    }
    eepromIndex = 0; // Reinicia o índice
    Serial.println("EEPROM apagada.");
    bluetooth.println("EEPROM apagada.");
  } else if (command == "STATUS") {
    int usage = (eepromIndex * 100) / eepromMax;
    bluetooth.print("EEPROM Usage: ");
    bluetooth.print(usage);
    bluetooth.println("%");
    if (usage >= 90) {
      bluetooth.println("EEPROM quase cheia! Envie 'ERASE' para apagar.");
    }
  } else {
    bluetooth.println("Comando desconhecido.");
  }
}

// Função para ler os sensores
void readSensors() {
  humidity = dht.readHumidity();
  temperature = dht.readTemperature();
  gasVal = analogRead(0); // Sensor de gás no pino A0
  alcVal = analogRead(1); // Sensor de álcool no pino A1
  heatIndex = computeHeatIndex(temperature, humidity);

  if (isnan(humidity) || isnan(temperature)) {
    Serial.println("Erro ao ler o sensor DHT!");
    bluetooth.println("Erro ao ler o sensor DHT!");
    return;
  }

  Serial.print("Timestamp: ");
  Serial.println(timestamp);
  Serial.print("Temperatura: ");
  Serial.print(temperature);
  Serial.println(" °C");
  Serial.print("Humidade: ");
  Serial.print(humidity);
  Serial.println(" %");
  Serial.print("Gás: ");
  Serial.println(gasVal);
  Serial.print("Álcool: ");
  Serial.println(alcVal);
  Serial.print("Heat Index: ");
  Serial.println(heatIndex);
}

// Função para verificar os valores
void checkValues() {
  bool allNormal = true;

  // Verifica cada condição
  if (temperature < TEMP_MIN || temperature > TEMP_MAX) {
    Serial.print("Temperatura fora do normal: ");
    Serial.print(temperature);
    Serial.println(" °C");
    allNormal = false;
  }

  if (humidity < HUM_MIN || humidity > HUM_MAX) {
    Serial.print("Humidade fora do normal: ");
    Serial.print(humidity);
    Serial.println(" %");
    allNormal = false;
  }

  if (gasVal > GAS_MAX) {
    Serial.print("Nível de gás fora do normal: ");
    Serial.println(gasVal);
    allNormal = false;
  }

  if (alcVal > ALC_MAX) {
    Serial.print("Nível de álcool fora do normal: ");
    Serial.println(alcVal);
    allNormal = false;
  }

  // Atualiza os LEDs e buzzer conforme os valores
  if (allNormal) {
    Serial.println("Todos os valores estão dentro do normal.");
    digitalWrite(LED_GREEN, HIGH); // Liga LED verde
    digitalWrite(LED_RED, LOW);    // Desliga LED vermelho
    noTone(BUZZERPIN);             // Garante que o buzzer esteja desligado
  } else {
    digitalWrite(LED_GREEN, LOW);  // Desliga LED verde
    digitalWrite(LED_RED, HIGH);   // Liga LED vermelho
    tone(BUZZERPIN, 1000, 500);    // Emite som no buzzer
  }
}

// Função para salvar dados na EEPROM
void saveToEEPROM() {
  int usage = (eepromIndex * 100) / eepromMax;

  if (eepromIndex + timestamp.length() + 5 < eepromMax) {
    for (int i = 0; i < timestamp.length(); i++) {
      EEPROM.write(eepromIndex++, timestamp[i]);
    }
    EEPROM.write(eepromIndex++, '|');
    EEPROM.write(eepromIndex++, (int)temperature);
    EEPROM.write(eepromIndex++, (int)humidity);
    EEPROM.write(eepromIndex++, gasVal);
    EEPROM.write(eepromIndex++, alcVal);

    Serial.println("Dados salvos na EEPROM.");
  } else {
    tone(BUZZERPIN, 1000, 500);
    Serial.println("EEPROM cheia! Envie o comando 'ERASE'.");
    bluetooth.println("EEPROM cheia! Envie o comando 'ERASE'.");
  }

  if (usage >= 90) {
    digitalWrite(LED_GREEN, LOW);
    digitalWrite(LED_RED, HIGH);
    Serial.println("EEPROM quase cheia!");
  } 
}

// Função para calcular o índice de calor
float computeHeatIndex(float temperature, float humidity) {
  float T = (temperature * 1.8) + 32;
  float H = humidity;
  float HI = -42.379 + 2.04901523 * T + 10.14333127 * H - 0.22475541 * T * H
             - 0.00683783 * T * T - 0.05481717 * H * H + 0.00122874 * T * T * H
             + 0.00085282 * T * H * H - 0.00000199 * T * T * H * H;
  return (HI - 32) / 1.8;
}